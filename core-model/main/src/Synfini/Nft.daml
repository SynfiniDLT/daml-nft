-- Copyright (c) 2022 ASX Operations Pty Ltd. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Synfini.Nft where

import DA.Assert ((===))
import DA.Set (Set)
import qualified DA.Set as Set

data TokenId = TokenId
  with
    signatories : Set Party
    value : Text
  deriving (Show, Eq, Ord)

data WalletId = WalletId
  with
    signatories : Set Party
    value : Text
  deriving (Show, Eq, Ord)

data Wallet = Wallet
  with
    id : WalletId
    provider : Party
    owner : Party
  deriving (Show, Eq)

template Nft
  with
    tokenId : TokenId
    issuer : Party
    wallet : Wallet
    observers : Set Party
  where
    signatory tokenId.signatories, wallet.id.signatories
    observer observers

    key tokenId : TokenId
    maintainer key.signatories

-- | Rule to allow the owner of a particular NFT to transfer it to any other wallet, provided the recipient wallet ID's
-- signatories are any subset of the NFT contract's signatories. For example, to transfer to another wallet belonging
-- to the same owner or with the same provider, or if the wallet ID's signatories is empty.
template FreeTransferRule
  with
    tokenId : TokenId
    wallet : Wallet
    observers : Set Party
  where
    signatory tokenId.signatories, wallet.id.signatories
    observer wallet.owner, observers

    key tokenId : TokenId
    maintainer key.signatories

    choice FreeTransferRule_Transfer : (ContractId Nft, ContractId FreeTransferRule)
      with
        recipientWallet : Wallet
      controller wallet.owner
      do
        (nftCid, nft) <- fetchByKey @Nft tokenId
        nft.wallet === wallet
        archive nftCid
        newNftCid <- create nft with wallet = recipientWallet
        newTransferRuleCid <- create this with wallet = recipientWallet
        pure (newNftCid, newTransferRuleCid)

-- | Rule to allow the owner of a particular NFT to transfer it, but only if the recipient is willing to accept it
-- via a 'WalletAcquisitionRule'.
template WalletControlledTransferRule
  with
    tokenId : TokenId
    wallet : Wallet
    observers : Set Party
  where
    signatory tokenId.signatories, wallet.id.signatories
    observer observers

    key tokenId : TokenId
    maintainer key.signatories

    choice WalletControlledTransferRule_Transfer : (ContractId Nft, ContractId WalletControlledTransferRule)
      with
        recipientWallet : Wallet
      controller wallet.owner
      do
        (nftCid, nft) <- fetchByKey @Nft tokenId
        nft.wallet === wallet
        archive nftCid
        let newNft = nft with wallet = recipientWallet
        newNftCid <- exerciseByKey @WalletAcquisitionRule wallet.id WalletAcquisitionRule_Acquire with ctrl = wallet.owner, nft = newNft
        newTransferRuleCid <- create this with wallet = recipientWallet
        pure (newNftCid, newTransferRuleCid)

-- | Rule to allow a wallet to receive NFTs from other parties. This may be a long-lived contract or may be created and
-- archived within a single transaction, such as in a "propose-accept" workflow.
template WalletAcquisitionRule
  with
    wallet : Wallet
    ctrls : Set Party -- ^ Parites permitted to send NFTs to this wallet.
    observers : Set Party
  where
    signatory wallet.id.signatories
    observer wallet.owner, ctrls, observers

    key wallet.id : WalletId
    maintainer key.signatories

    nonconsuming choice WalletAcquisitionRule_Acquire : ContractId Nft
      with
        ctrl : Party
        nft : Nft
      controller ctrl, nft.tokenId.signatories
      do
        assertMsg "Controller must be authorized to send nft" $ ctrl `Set.member` ctrls
        nft.wallet === wallet
        create nft
